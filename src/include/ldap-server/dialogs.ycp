/**
 * File:	include/ldap-server/dialogs.ycp
 * Package:	Configuration of ldap-server
 * Summary:	Dialogs definitions
 * Authors:	Andreas Bauer <abauer@suse.de>
 *
 * $Id$
 */

{

textdomain "ldap-server";

import "CWMFirewallInterfaces";
import "Label";
import "Wizard";
import "Ldap";
import "LdapPopup";
import "LdapServer";
import "HTML";
import "Report";

include "ldap-server/helps.ycp";
include "ldap-server/tree_structure.ycp";

string section_id = "";

                 /*heading for all dialogs*/
string caption = _("LDAP Server Configuration");


/**********************
 ** helper functions **
 **********************/

string error_str = "";

list<map> generateTreeRec( list<map> tree, string parent, list<string> children )
{
    y2milestone( "generating tree for item '%1', children are '%2'", parent, children );
    foreach( string item, children, {
        map<string,any> item_map = widget_map[item]:nil;
        if( item_map == nil )
        {
            error_str = "item "+item+" does not exist but is referenced by '"+parent+"'!";
            return nil;
        }
        y2milestone( "adding tree item '%1' to parent %2", item, parent );
        tree = Wizard::AddTreeItem( tree, parent, (string)item_map["name"]:"", item );
        y2milestone( "tree '%1'", tree );
        if( haskey( item_map, "children" ) )
        {
            list<string> childlist = (list<string>)item_map["children"]:[];
            tree = generateTreeRec( tree, item, childlist );
            if( tree == nil ) return nil;
        }
    } );
    return tree;
}

list<map> generateTree()
{
    list<string> baselist = (list<string>)widget_map["base","children"]:[];
    list<map> tree = [];
    y2debug( "generating tree for 'base', children are '%1'", baselist );
    foreach( string item, baselist, {
        map<string,any> item_map = widget_map[item]:nil;
        if( item_map == nil )
        {
            error_str = "item "+item+" does not exist but is referenced by 'base'!";
            return nil;
        }
        y2milestone( "adding tree item '%1' to root", item );
        tree = Wizard::AddTreeItem( tree, "", (string)item_map["name"]:"", item );
        if( haskey( item_map, "children" ) )
        {
            list<string> childlist = (list<string>)item_map["children"]:[];
            tree = generateTreeRec( tree, item, childlist );
            if( tree == nil ) return nil;
        }
    } );

    //create dynamic tree items (databases)
    return tree;
}

boolean callHandler( string item, string handler )
{
    y2milestone("callhandler %1 for item %2", handler, item );
    if( haskey( widget_map[item]:$[], handler ) )
    {
        any function = widget_map[item,handler]:nil;
        if( function != nil )
        {
            if( !(boolean)eval( function ) )
            {
                if (callback_error != "" )
                {
                    Report::Error( callback_error );
                }
                return false;
            }
        } else {
            y2error( sformat ("LdapServer Module: illegal handler '%1' for item '%2'",
			      handler, item));
        }
    }
    return true;
}

string showTreeDialog( string name, boolean focus_tree )
{
    //create new item
    term widget = nil;
    if ( !LdapServer::ReadServiceRunning() )
    {
        if ( name != "daemon" ) {
            symbol ret = Popup::AnyQuestion3( _("The LDAP Server is not running."),
                    _("Do you want to start it now to re-read its configuration data or do you want to create a new configuration from scratch?"),
                    _("Restart"),
                    _("New Configuration"),
                    Label::AbortButton(),
                    `focus_yes );
            name = "daemon";
            if ( ret == `yes )
            {
                return "__reread__";
            }
            else if ( ret == `no )
            {
                return "__empty__";
            }
            else if ( ret == `retry )
            {
                return name;
            }
        }
    }
    widget = (term)widget_map[name,"widget"]:`Label( "Loading widget for item '"+name+"' failed." );

    //get helps page
    string help_page = (string)widget_map[name,"help_page"]:name;
    string help_string = HELPS[help_page]:(_("help page for item <b>")+help_page+_("</b> not available"));

    Wizard::SetAbortButton(`abort, Label::CancelButton());
    Wizard::SetContentsButtons( caption, widget, help_string,
        Label::BackButton(), Label::OKButton() );
    Wizard::HideBackButton();


    if( focus_tree ) UI::SetFocus( `id( `wizardTree ) );

    return name;
}


/**********************
 ** dialog functions **
 **********************/

term dlg_service_initial =
    `HSquash(
        `VBox(
            `Heading( _("General Settings") ),
            `VSpacing(),
            `VBox(
                `Frame( _("&Start LDAP Server"),
                    `VBox(
                        `RadioButtonGroup(
                            `VBox(
                                `Left( `RadioButton( `id( `rb_yes ), `opt( `notify ),
                                            Label::YesButton(),
                                            false ) ),
                                `Left( `RadioButton( `id( `rb_no ), `opt( `notify ),
                                            Label::NoButton(),
                                            true ) )
                            )
                        ),
                        `Left( `CheckBox( `id( `cb_register_slp ),
                                    _("Register at an &SLP Daemon"),
                                    LdapServer::ReadSLPEnabled() )),
                        `HStretch()
                    )
                )
            ),
            `VSpacing(),
            `VBox(
                `Frame( _("Firewall Settings"),
                    `VBox(
                        `ReplacePoint( `id( `rp_firewall ),
                            `Empty()
                        ),
                        `HStretch()
                    )
                )
            )
        )
    );

any EnableServiceDialog()
{
    map <string, any> defaults = LdapServer::CreateInitialDefaults();
    map<string, any> firewall_settings = $[
            "services": [ "service:openldap" ],
            "display_details": true,
         ];
    map<string, any> firewall_widget =
	CWMFirewallInterfaces::CreateOpenFirewallWidget (firewall_settings);

    Wizard::SetContentsButtons( caption, dlg_service_initial,
                                HELPS["service_dialog"]:"help not found",
                                Label::BackButton(), Label::NextButton() );
    Wizard::HideBackButton();
    Wizard::SetAbortButton(`abort, Label::CancelButton());

    UI::ReplaceWidget(`rp_firewall,
            firewall_widget["custom_widget"]:`Empty()
    );
    CWMFirewallInterfaces::OpenFirewallInit (firewall_widget, "");

    if( (boolean)defaults["serviceEnabled"]:true )
    {
        UI::ChangeWidget( `rb_yes, `Value, true );
        Wizard::SetNextButton( `next, Label::NextButton());
    }
    else
    {
        UI::ChangeWidget( `cb_register_slp, `Enabled, false );
        Wizard::SetNextButton( `finish , Label::FinishButton());
    }
    UI::ChangeWidget( `cb_register_slp, `Value, defaults["slpRegister"]:false );

    any ret = nil;
    map event = $[];
    while( true )
    {
        event = UI::WaitForEvent ();
        ret = event["ID"]:nil;
        CWMFirewallInterfaces::OpenFirewallHandle (firewall_widget, "", event);
        y2milestone( "EnableServiceDialog: seeing return value '%1'", ret );

        if( ret == `back || ret == `abort || ret == `cancel) break;
        else if( ret == `next || ret == `finish )
        {
            CWMFirewallInterfaces::OpenFirewallStore (firewall_widget, "", event);
            if ( (boolean)UI::QueryWidget( `cb_register_slp, `Value ) )
            {
                defaults["slpRegister"] = 1;
            }
            else
            {
                defaults["slpRegister"] = 0;
            }
            LdapServer::SetInitialDefaults(defaults);
            break;
        }
        else if( ret == `rb_yes )
        {
            defaults["serviceEnabled"] = true;
            UI::ChangeWidget( `cb_register_slp, `Enabled, true );
            Wizard::SetNextButton( `next , Label::NextButton());
        } else if( ret == `rb_no )
        {
            defaults["serviceEnabled"] = false;
            UI::ChangeWidget( `cb_register_slp, `Enabled, false );
            Wizard::SetNextButton( `finish , Label::FinishButton());
        }
    }

    return ret;
}

any ServerTypeDialog()
{
    term serverTypeWidget =
        `HSquash(
            `VBox(
                `Heading( _("Server Type") ),
                `Frame("",
                    `VBox(
                        `VSpacing(),
                        `RadioButtonGroup(
                            `id( `rbg_servertype ),
                            `VBox(
                                `Left(
                                    `RadioButton(
                                        `id( `rb_standalone ),
                                        _("Stand-alone server"),
                                        true
                                    )
                                ),
                                `VSpacing(),
                                `Left(
                                    `RadioButton(
                                        `id( `rb_master ),
                                        _("Master server in a replication setup"),
                                        false
                                    )
                                ),
                                `VSpacing(),
                                `Left(
                                    `RadioButton(
                                        `id( `rb_slave ),
                                        _("Replica (slave) server.\n") +
                                        _("All data, including configuration, is replicated from a remote server."),
                                        false
                                    )
                                )
                            )
                        )
                    )
                )
            )
        );
    Wizard::SetContentsButtons( caption,
            serverTypeWidget,
            HELPS["server_type"]:"help not found",
            Label::BackButton(),
            Label::NextButton() );
    any ret = nil;
    while ( true )
    {
        ret = UI::UserInput();
        y2milestone( "TlsConfigDialog: seeing return value '%1'", ret );
        if ( ret == `next )
        {
            if (UI::QueryWidget( `id( `rbg_servertype ), `CurrentButton) == `rb_slave )
            {
                ret = `slave_setup;
                LdapServer::WriteSetupMaster(false);
                LdapServer::WriteSetupSlave(true);
            }
            else if (UI::QueryWidget( `id( `rbg_servertype ), `CurrentButton) == `rb_master )
            {
                if ( size( LdapServer::ReadHostnameFQ() ) == 0 )
                {
                    Popup::Notify(_("YaST was not able to determine the fully qualified hostname of this
computer. 
") +
                                  _("Setting up a replication master is currently not possible.") );
                    UI::ChangeWidget( `rb_master, `Enabled, false );
                    UI::ChangeWidget( `rbg_servertype, `CurrentButton, `rb_standalone );
                    continue;
                }
                else
                {
                    LdapServer::WriteSetupMaster(true);
                    LdapServer::WriteSetupSlave(false);
                }
            }
            else
            {
                LdapServer::WriteSetupMaster(false);
                LdapServer::WriteSetupSlave(false);
            }
            SCR::Execute(.ldapserver.reset);
        }
        return ret;
    }
    return ret;
}

any TlsConfigDialog()
{
    Wizard::SetContentsButtons( caption,
            tlsWidget,
            HELPS["tls_dialog"]:"help not found",
            Label::BackButton(),
            Label::NextButton() );
    LdapServer::InitGlobals();
    cb_read_tls();
    any ret = nil;
    while( true )
    {
        ret = UI::UserInput();
        y2milestone( "TlsConfigDialog: seeing return value '%1'", ret );

        if( ret == `back )
        {
            break;
        }
        if( ret == `abort || ret == `cancel )
        {
	    if( Popup::ReallyAbort(true) )
            {
                break;
            }
	    else
            {
                continue;
            }
        }
        else if( ret == `next || ret == `finish )
        {
            if (! cb_write_tls() )
            {
                Report::Error( callback_error );
                continue;
            }
            break;
        }
        else if( is( ret, symbol ) )
        {
            handler_cmd = (symbol)ret;
            cb_input_tls();
        }
    }

    return ret;

}

any ProposalDialog()
{
    symbol ret = LdapDatabase::AddDbBasic(true);
    if ( ret == `next )
    {
        LdapServer::SetInitialDefaults(LdapDatabase::GetDatabase());
        LdapServer::WriteLdapConfBase( LdapDatabase::GetLdapConfBase() );
        if ( LdapServer::ReadSetupMaster() )
        {
            ret = `mastersetup;
        }
    }

    return ret;
}

any TreeDialog()
{
    //close service dialog
//    UI::CloseDialog();
    Wizard::CreateTreeDialog();
    Wizard::SetDesktopTitleAndIcon("ldap-server");
    //item selected at start
    current_tree_item = "daemon";

    //trigger initial build of widget tree
    rebuild_widget_tree = true;

    any ret = nil;
    while( true )
    {
        if( rebuild_widget_tree )
        {
            //generate tree
            Wizard::DeleteTreeItems();
            deleteDynamicTreeItems();
            if ( LdapServer::ReadServiceRunning() )
            {
                generateDynamicTreeItems();
            }
            widget_tree = generateTree();
            if( widget_tree == nil )
            {
                y2error( "error when generating widget tree: %1", error_str );
            }

            /* tree widget headline */
            Wizard::CreateTree( widget_tree, _("Configuration:") );

            //select&show current item
            current_tree_item = showTreeDialog( current_tree_item, ( widget_tree == [] ) ? false : true );
            Wizard::SelectTreeItem( current_tree_item );
            //initialize current dialog
            callHandler( current_tree_item, "cb_read" );
            rebuild_widget_tree = false;
        }
        map event = UI::WaitForEvent();
        ret = event["ID"]:nil;
        y2milestone( "TreeDialog: seeing return value %1", ret );

        if( is( ret, string ) || ret == `wizardTree )
        {
            string new_item = Wizard::QueryTreeItem();

            // workaround to catch changes in the firewall widget
            if ( is( ret, string) && ( issubstring( (string)ret, "firewall" ) ) )
            {
                CWMFirewallInterfaces::OpenFirewallHandle (fw_widget, "", event);
                continue;
            }

            //check values of current tree item
            y2milestone( "wizard-->current item is %1", current_tree_item );

            if( !callHandler( current_tree_item, "cb_check" ) )
            {
                Wizard::SelectTreeItem( current_tree_item );
                continue;
            }

            if( !callHandler( current_tree_item, "cb_write" ) )
            {
                Wizard::SelectTreeItem( current_tree_item );
                continue;
            }

            current_tree_item = showTreeDialog( new_item, (ret == `wizardTree) ? true : false );
            if ( current_tree_item == "__reread__" )
            {
                ret = `reread;
                break;
            }
            if ( current_tree_item == "__empty__" )
            {
                ret = `empty;
                break;
            }
            Wizard::SelectTreeItem( current_tree_item );

            callHandler( current_tree_item, "cb_read" );

        }
        else if( is( ret, symbol ) )
        {
            symbol sym_ret = (symbol)ret;
            if( sym_ret == `abort || sym_ret == `cancel)
            {
                if( Popup::ReallyAbort(true) )
                {
                    break;
                }
                else
                {
                    continue;
                }
            }
            else if ( sym_ret == `back || sym_ret == `reread ) break;
            else if( sym_ret == `next )
            {
                if( !callHandler( current_tree_item, "cb_check" ) ) continue;
                if( !callHandler( current_tree_item, "cb_write" ) ) continue;
                break;
            }
            else if( haskey( widget_map[current_tree_item]:$[], "cb_input" ) )
            {
                //call input handler of current tree item
                any function = widget_map[current_tree_item,"cb_input"]:nil;
                if( function != nil )
                {
                    //############## input handler ################
                    handler_cmd = sym_ret;
                    if( !(boolean)eval( function ) )
                    {
                        Report::Error( callback_error );
                        continue;
                    }
                } else
                {
                    Report::Error( "LdapServer Module: illegal input handler for item '"+current_tree_item+"'" );
                }
            }
        }
    }
    return ret;
}

string VerifyAdminPasswordPopup( map<string,any> bindparm, string suffix )
{
    map<string,string> authinfo = LdapServer::ReadAuthInfo(suffix);
    string pw = nil;
    if ( size(authinfo) > 0 )
    {
        if (authinfo["bind_dn"]:"1" == bindparm["bind_dn"]:"2" )
        {
            pw = authinfo["bind_pw"]:"";
        }
    }
    Ldap::Set(bindparm);
    Ldap::LDAPInitWithTLSCheck($[]);
    symbol state= `cont;
    while ( state == `cont )
    {
        if ( pw == nil )
        {
            pw =  Ldap::GetLDAPPassword(false);
        }
        if ( pw == nil )
        {
            state = `cancel;
        }
        else
        {
            string err = Ldap::LDAPBind( pw );
            if ( err != "" )
            {
                pw = nil;
                if( ! Popup::YesNo( _("LDAP Authentication failed. Try again?\n") +
                                    _("Error message: ") + err  ) )
                {
                    state = `cancel;
                }
            }
            else
            {
                state= `ok;
                LdapServer::WriteAuthInfo( suffix, $[ "bind_dn" : bindparm["bind_dn"]:"", "bind_pw" : pw ] );
            }
        }
    }
    return pw;
}

map <string,any> SyncreplAccountConfig( map<string,any> syncrepl )
{
    term widget =
        `HSquash(
            `VBox(
                `RadioButtonGroup( `id(`rbg_syncaccount),
                    `VBox(
                        `Left(
                            `RadioButton( `id( `rb_createaccount ), `opt(`notify), _("Create new account in the first database"), true )
                        ),
                        `Left(
                            `HBox(
                                `HSpacing(2),
                                `VBox( `id(`vb_account_param),
                                    `Left(
                                        `HBox(
                                            `InputField( `id( `if_uid ), `opt( `hstretch ), _("User Id"), "" ),
                                            `HSpacing(0.5),
                                            `InputField( `id( `if_basedn ), `opt( `hstretch ), _("Container Object"), "" ),
                                            `HSpacing(0.5),
                                            `VBox(
                                                `VSpacing(0.5),
                                                `PushButton( `id( `pb_select_parent ), _("Browse") )
                                            )
                                        )
                                    ),
                                    `Left(
                                        `CheckBox( `id( `cb_random_pw ), _("Generate a Random Password"), true)
                                    )
                                )
                            )
                        ),
                        `VSpacing(0.3),
                        `Left(
                            `RadioButton( `id( `rb_useconfig ), `opt(`notify), _("Use the \"cn=config\" Account for Replication") )
                        )
                    )
                )
            )
        );

    map <string,any> db = LdapServer::ReadDatabase(1);
    string suffix = (string)db["suffix"]:"";
    string rootdn = (string)db["rootdn"]:"";

    Wizard::CreateDialog();
    Wizard::SetDesktopTitleAndIcon("ldap-server");
    Wizard::SetContents( _("Configure Account for Replication"), widget,
			     "",
			     true, true);

    UI::ChangeWidget( `if_uid, `Value, "syncrepl");
    UI::ChangeWidget( `if_basedn, `Value, "ou=system," + suffix );
    any ret = nil;
    while (true)
    {
        ret = UI::UserInput();
        if ( ret == `rb_useconfig )
        {
            UI::ChangeWidget( `vb_account_param, `Enabled, false );
        }
        else if ( ret == `rb_createaccount )
        {
            UI::ChangeWidget( `vb_account_param, `Enabled, true );
        }
        else if (ret == `pb_select_parent )
        {
            map <string,any> client = $[
                    "bind_dn" : rootdn,
                    "ldap_server"  : syncrepl["provider","target"]:"",
                    "ldap_tls" : syncrepl["starttls"]:true
                ];
            string pw = VerifyAdminPasswordPopup( client, suffix );
            if ( pw  != nil )
            {
                string dn = LdapPopup::BrowseTree(suffix);
                if ( dn != "" )
                {
                    UI::ChangeWidget( `if_basedn, `Value, dn );
                }
            }
        }
        if ( ret == `next )
        {
            string binddn = "";
            string bindpw = "";
            if ( (boolean)UI::QueryWidget( `rb_createaccount, `Value ) )
            {
                binddn = "uid=" + (string)UI::QueryWidget( `if_uid, `Value ) + "," + (string)UI::QueryWidget( `if_basedn , `Value);
                if ( ! (boolean)LdapServer::ValidateDn( binddn ) )
                {
                    Popup::Error( "\"" + binddn + "\"" + _("is not a valid LDAP DN") );
                    continue;
                }
                if ( (boolean)UI::QueryWidget( `cb_random_pw, `Value ) )
                {
                   bindpw = LdapServer::GenerateRandPassword();
                }
                else
                {
                    term askPwWidget =
                        `HSquash(
                            `VSquash(
                                `VBox(
                                    `Password( `id( `te_pw ), `opt(`hstretch), _("Password") ),
                                    `HSpacing( 0.5 ),
                                    `Password( `id( `te_valid_pw ), `opt(`hstretch), _("Validate Password") ),
                                    `HSpacing( 0.5 ),
                                    Wizard::CancelOKButtonBox()
                                )
                            )
                        );
                    UI::OpenDialog( `opt(`decorated), askPwWidget );
                    any ret1 = nil;
                    while ( true )
                    {
                        ret1 = UI::UserInput();
                        if (ret1 == `cancel )
                        {
                            bindpw = "";
                            break;
                        }
                        else if ( ret1 == `ok )
                        {
                            string pw = (string)UI::QueryWidget( `te_pw, `Value );
                            string verifypw = (string)UI::QueryWidget( `te_valid_pw, `Value );
                            if ( size(pw) == 0 )
                            {
                                Popup::Error( _("Enter a password") );
                                UI::ChangeWidget( `te_pw, `Value, "" );
                                UI::ChangeWidget( `te_valid_pw, `Value, "" );
                            }
                            else if ( pw == verifypw )
                            {
                                bindpw = pw;
                                break;
                            }
                            else
                            {
                                Popup::Error( _("The passwords you have entered do not match. Try again.") );
                                UI::ChangeWidget( `te_pw, `Value, "" );
                                UI::ChangeWidget( `te_valid_pw, `Value, "" );
                            }
                        }
                    }
                    UI::CloseDialog();
                    if ( ret1 != `ok)
                    {
                        continue;
                    }
                }
                LdapServer::WriteSyncReplAccount( $[ "dn" : binddn, "pw" : bindpw, "dbsuffix" : suffix] );
                syncrepl["binddn"] = binddn;
                syncrepl["credentials"] = bindpw;
            }
            else
            {
                syncrepl["binddn"] = "cn=config";
            }
            break;
        }
    }
    Wizard::CloseDialog();
    return syncrepl;
}

any SlaveSetupDialog()
{
    term widget =
        `HSquash(
            `VBox(
                `Heading( _("Provider Details") ),
                `VSpacing(0.3),
                `VSpacing(),
                `VSquash(
                    `HBox(
                        `ComboBox( `id( `cb_sync_prot ), `opt(`notify), _("Protocol"), [ "ldap", "ldaps" ] ),
                        `HSpacing(),
                        `InputField( `id( `te_sync_target ), `opt(`hstretch), _("Provider Hostname"), "" ),
                        `HSpacing(),
                        `HSquash(
                            `IntField( `id(`if_sync_port), "Port", 0, 65536, 389)
                        ),
                        `HSpacing(),
                        `VBox(
                            `Bottom(
                                `CheckBox( `id( `cb_start_tls ), _("Use StartTLS"), true )
                            ),
                            `VSpacing(0.3)
                        )
                    )
                ),
                `VSpacing(0.3),
                `Password( `id( `te_config_cred ), `opt(`hstretch), _("Administration Password for the \"cn=config\" Database"), "" ),
                `VSpacing(0.3),
                `VSquash(
                    `HBox(
                        `InputField( `id( `te_ca_file ), `opt( `hstretch ), _("C&A Certificate File (PEM Format)") ),
                        `HSpacing( 0.5 ),
                        `Bottom(
                            `PushButton( `id( `pb_ca_file ), _("Bro&wse...") )
                        )
                    )
                )
            )
        );

    Wizard::SetContentsButtons( caption,
            widget,
            HELPS["slave_dialog"]:"help not found",
            Label::BackButton(),
            Label::NextButton() );
    any ret = nil;
    string cacert = "/etc/ssl/certs/YaST-CA.pem";

    while ( true )
    {
        UI::ChangeWidget(`cb_start_tls, `Enabled, false );
        UI::ChangeWidget(`te_ca_file, `Value, cacert );
        map synbase = LdapServer::ReadSyncreplBaseConfig();
        if ( size(synbase) > 0 )
        {
            UI::ChangeWidget( `cb_sync_prot, `Value, (string)synbase["provider","protocol"]:"" );
            UI::ChangeWidget( `te_sync_target, `Value, (string)synbase["provider","target"]:"" );
            UI::ChangeWidget( `if_sync_port, `Value, (integer)synbase["provider","port"]:389 );
            UI::ChangeWidget( `cb_start_tls, `Value, (boolean)synbase["start_tls"]:true );
            UI::ChangeWidget( `te_config_cred, `Value, (string)synbase["credentials"]:"");
            LdapServer::WriteSyncreplBaseConfig( $[] );

            ret = `next;
        }
        else
        {
            ret = UI::UserInput();
        }
        y2milestone( "SlaveSetupDialog: seeing return value '%1'", ret );
        if( ret == `pb_ca_file )
        {
            string name = UI::AskForExistingFile( "/etc/ssl/certs", "*.pem *.crt *", _("Select CA Certificate File") );
            if( name != nil )
            {
                cacert = name;
            }
            continue;
        }
        else if ( ret == `next )
        {
            if ( cacert == "" || cacert == nil )
            {
                Popup::Error( _("Select a Valid CA Certificate File") );
                continue;
            }

            // test connection
            map<string,any> provider = $[
                    "protocol" : (string)UI::QueryWidget( `cb_sync_prot, `Value ),
                    "target"   : (string)UI::QueryWidget( `te_sync_target, `Value ),
                    "port"     : (integer)UI::QueryWidget( `if_sync_port, `Value )
                ];

            map<string,any> testparm = $[];
            testparm = add(testparm, "target", provider );
            if ( provider["protocol"]:"ldap" == "ldap" )
            {
                testparm = add(testparm, "starttls", true );
            }
            else
            {
                testparm = add(testparm, "starttls", false );
            }
            testparm = add(testparm, "basedn", "cn=config" );
            testparm = add(testparm, "binddn", "cn=config" );
            testparm = add(testparm, "credentials", (string)UI::QueryWidget(`te_config_cred, `Value) );
            if ( cacert != "" &&  cacert != nil )
            {
                testparm = add( testparm, "cacert", cacert );
            }

            if (! LdapServer::ReadModeInstProposal() ) // Doing these checks during installation will 
                                                       // most probably fail
            {
                if (!(boolean) LdapServer::InitRemoteConnection( testparm ) )
                {
                    map<string,any> err = LdapServer::ReadError();
                    Popup::ErrorDetails( _("Failed to open connection to the \"cn=config\" database on the provider server.\n") +
                             _("Verify that the provider server allows remote connections to the 
\"cn=config\" database and that you entered the correct password.
"),
                             _("The following error messages were returned:") + "\n\n\"" +
                             (string)err["msg"]:"" + "\"\n\"" +
                             (string)err["details"]:"" + "\"" );
                    continue;
                }
                if (!(boolean) LdapServer::VerifyTlsSetup( testparm["target"]:$[] ) )
                {
                    map<string,any> err = LdapServer::ReadError();
                    Popup::ErrorDetails( _("An error occurred while verifying the TLS/SSL configuration."),
                                         (string)err["msg"]:"" + "\n" + (string)err["details"]:"" + "\n" );
                    if (Popup::YesNo( _("Do you want to import a different CA/Server Certificate?") ) )
                    {
                        WFM::CallFunction("common_cert", [] );
                    }
                    continue;
                }
                // Check if the syncrepl config of cn=config makes sense
                list<map<string,any> > srl = LdapServer::ReadSyncRepl(0);
                map <string,any> syncrepl = srl[0]:$[];
                if ( size(syncrepl) == 0 )
                {
                    if (Popup::ContinueCancel( _("The replication configuration on the provider server is missing.\n") +
                                               _("Click Continue to create it now.") ) )
                    {
                        syncrepl = $[
                                "provider" : testparm["target"]:$[],
                                "starttls" : testparm["starttls"]:true,
                                "binddn"   : testparm["binddn"]:"cn=config",
                                "credentials" : testparm["credentials"]:"",
                                "type"     : "refreshAndPersist"
                            ];
                    }
                    else
                    {
                        ret = `cancel;
                        break;
                    }
                }
                else
                {
                    // 1. Verify that the provider uri points to the provider itself
                    if ( size(srl) == 1 ) // this test needs only be done in a non-MirrorMode setup
                    {
                        map<string, any> provider = syncrepl["provider"]:$[];
                        boolean setupok=true;
                        if ( size(provider) > 0 )
                        {
                            if ( (string)provider["target"]:"" != (string)testparm["target","target"]:"" )
                            {
                                y2error("Provider target names do not match: <%1> vs. <%2>",
                                        (string)provider["target"]:"",(string)testparm["target","target"]:"");
                                setupok=false;
                            }
                            if ( (string)provider["protocol"]:"" != (string)testparm["target","protocol"]:"" )
                            {
                                y2error("Provider protocols do not match: <%1> vs. <%2>",
                                        (string)provider["protocol"]:"",(string)testparm["target","protocol"]:"");
                                setupok=false;
                            }
                            if ( (integer)provider["port"]:0 != (integer)testparm["target","port"]:0 )
                            {
                                y2error("Provider ports do not match: <%1> vs. <%2>",
                                        (string)provider["port"]:"",(string)testparm["target","port"]:"");
                                setupok=false;
                            }
                            if ( ! setupok )
                            {
                                Popup::Error( _("The replication configuration on the master server indicates that
it is already acting as a replication consumer.
") +
                                              _("Setting up cascaded replication of the cn=config is currently not supported.") );
                                ret = `cancel;
                                break;
                            }
                        }
                    }
                    // 2. Verify that the binddn/credential combination acutally works
                    map <string,any> bindtestparm = $[
                                "target" : syncrepl["provider"]:$[],
                                "starttls" : syncrepl["starttls"]:true,
                                "basedn" : syncrepl["basedn"]:"",
                                "binddn" : syncrepl["binddn"]:"",
                                "credentials" : syncrepl["credentials"]:""
                            ];
                    if(!(boolean)SCR::Execute( .ldapserver.remoteBindCheck, bindtestparm ) )
                    {
                        map<string,any> err = SCR::Error(.ldapserver);
                        if ( Popup::ContinueCancel( _("Checking the authentication credentials defined in the replication configuration on the provider server failed.\n") +
                                                    _("The test returned the following error messages:")
                                                    + "\n\n\"" +
                                                    (string)err["summary"]:"" + "\"\n\"" + (string)err["description"]:""
                                                    + "\"\n\n" +
                                                    _("Click \"Continue\" to correct this now.") ) )
                        {
                            syncrepl = SyncreplAccountConfig( syncrepl );
                            if ( syncrepl["binddn"]:"" == "cn=config" )
                            {
                                syncrepl["credentials"] = testparm["credentials"]:"";
                            }
                            else // Get admin password if we don't have it already
                            {
                                map <string,any> db = LdapServer::ReadDatabase(1);
                                string suffix = (string)db["suffix"]:"";
                                string rootdn = (string)db["rootdn"]:"";
                                if ( size( LdapServer::ReadAuthInfo(suffix) ) == 0 )
                                {
                                    map <string,string> authinfo_firstdb=$[];
                                    map <string,any> client = $[
                                            "bind_dn" : rootdn,
                                            "ldap_server"  : syncrepl["provider","target"]:"",
                                            "ldap_tls" : syncrepl["starttls"]:true
                                        ];
                                    VerifyAdminPasswordPopup( client, suffix );
                                }
                            }
                        }
                        else
                        {
                            ret = `cancel;
                            break;
                        }
                    }
                }
                LdapServer::WriteSyncreplBaseConfig( syncrepl );
                LdapServer::WriteAuthInfo( "cn=config", $[ "bind_dn" : "cn=config", "bind_pw" : testparm["credentials"]:"" ] );
                break;
            }
            else // we were called during the Installation Proposal
            {
                map<string,any> syncrepl = $[
                        "provider" : testparm["target"]:$[],
                        "starttls" : testparm["starttls"]:true,
                        "binddn"   : testparm["binddn"]:"cn=config",
                        "credentials" : testparm["credentials"]:"",
                        "type"     : "refreshAndPersist"
                    ];
                LdapServer::WriteSyncreplBaseConfig( syncrepl );
            }
        }
        if ( ret == `cb_sync_prot )
        {
            string prot = (string) UI::QueryWidget( `cb_sync_prot, `Value );
            integer port = (integer) UI::QueryWidget( `if_sync_port, `Value );
            if ( prot == "ldaps" )
            {
                UI::ChangeWidget( `cb_start_tls, `Value, false );
                if ( port == 389 )
                {
                    UI::ChangeWidget( `if_sync_port, `Value, 636 );
                }
            }
            else
            {
                UI::ChangeWidget( `cb_start_tls, `Value, true );
                if ( port == 636 )
                {
                    UI::ChangeWidget( `if_sync_port, `Value, 389 );
                }
            }
            continue;
        }
        break;
    }
    if ( ret != `next )
    {
        // reset remote connection
        SCR::Execute(.ldapserver.reset);
    }

    return ret;
}

/**
 * ReplicatonSummary dialog
 * @return dialog result
 */
any ReplicatonSetupSummaryDialog() {

    any ret = nil;

    LdapServer::SetupRemoteForReplication();
    LdapServer::ReadFromDefaults();
    ret = `next;
    return ret;
}

any MasterSetupDialog()
{
    term widget =
        `HSquash(
            `VSquash(
                `VBox(
                    `Heading( _("Replication Master setup") ),
                    `VSpacing( 0.5 ),
                    `Label( _("To act as a master server for replication, the configuration database needs
to be remotely accessible. Set a password for the configuration database.
") +
                            _("
(Remote access to the Configuration database will be restricted to encrypted
LDAP Connections.)
")
                    ),
                    `VSpacing( 0.5 ),
                    `Password( `id( `te_rootpw ), `opt( `hstretch ), _("Enter new &Password") ),
                    `VSpacing( 0.5 ),
                    `Password( `id( `te_valid_rootpw ), `opt( `hstretch ), _("&Validate Password") ),
                    `VSpacing( 0.5 ),
                    `Left(
                        `CheckBox( `id( `cb_mirror_mode ), _("Prepare for MirrorMode replication (generates the serverId attribute)") )
                    )
                )
            )
        );
    Wizard::SetContentsButtons( caption,
            widget,
            HELPS["master_setup_dialog"]:"help not found",
            Label::BackButton(),
            Label::NextButton() );
    any ret = nil;
    while ( true )
    {
        ret = UI::UserInput();
        if ( ret == `next )
        {
            string pw = (string)UI::QueryWidget( `te_rootpw, `Value );
            string verifypw = (string)UI::QueryWidget( `te_valid_rootpw, `Value );
            if ( size(pw) == 0 )
            {
                Popup::Error( _("Enter a password") );
                UI::ChangeWidget( `te_rootpw, `Value, "" );
                UI::ChangeWidget( `te_valid_rootpw, `Value, "" );
            }
            else if ( pw != verifypw )
            {
                Popup::Error( _("The passwords you have entered do not match. Try again.") );
                UI::ChangeWidget( `te_rootpw, `Value, "" );
                UI::ChangeWidget( `te_valid_rootpw, `Value, "" );
                pw = "";
                verifypw ="";
            }
            else
            {
                map <string, any> defaults = LdapServer::CreateInitialDefaults();
                defaults["configpw"] = pw;
                LdapServer::SetInitialDefaults(defaults);
                LdapServer::WriteSetupMirrorMode( (boolean)UI::QueryWidget( `cb_mirror_mode, `Value ) );
                break;
            }
        }
        else
        {
            break;
        }
    }
    return ret;
}

/* EOF */
}
